plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.9'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'nu.studer.jooq' version '8.2'
	id 'com.netflix.dgs.codegen' version '6.1.2'
}

group = 'com.sportsplatform'
version = '0.0.1-SNAPSHOT'
description = 'Auth-Service'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

repositories {
	mavenCentral()
}

ext {
	set('netflixDgsVersion', "10.4.0")
}

sourceSets {
	main {
		java {
			srcDirs += "build/generated/sources/dgs-codegen"
		}
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.security:spring-security-crypto'

	// GraphQL DGS
	implementation 'com.netflix.graphql.dgs:graphql-dgs-spring-graphql-starter'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-jooq'
	jooqGenerator 'org.postgresql:postgresql:42.7.4'

	// JWT
	implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'
	runtimeOnly 'org.postgresql:postgresql:42.7.4'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'com.netflix.graphql.dgs:graphql-dgs-spring-graphql-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

generateJava {
	schemaPaths = ["${projectDir}/src/main/resources/schema"]
	packageName = "com.sportsplatform.auth_service.graphql.generated"
	generateClient = false
}

jooq {
	configurations {
		main {
			generationTool {
				jdbc {
					driver = 'org.postgresql.Driver'
					url = 'jdbc:postgresql://localhost:5432/sports_user_db'
					user = 'sports_user'
					password = 'sports_pass'
				}

				generator {
					database {
						name = 'org.jooq.meta.postgres.PostgresDatabase'
						inputSchema = 'public'
					}

					target {
						packageName = 'com.sportsplatform.user.jooq'
						directory = 'build/generated-src/jooq/main'
					}
				}
			}
		}
	}
}

dependencyManagement {
	imports {
		mavenBom "com.netflix.graphql.dgs:graphql-dgs-platform-dependencies:${netflixDgsVersion}"
	}
}

tasks.named('test') {
	useJUnitPlatform()
}
